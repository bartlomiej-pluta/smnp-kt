import smnp.music.tools;
import smnp.text;
import smnp.io;
import smnp.system;

if(__param__.containsKey("help")) {
    println("Simple metronome");
    println();
    println("Optional parameters:");
    println("  - bpm: int [120]      - set BPM of metronome");
    println("  - beats: int [4]      - set time signature of metronome (4 = 4/4, 3 = 3/4 and so on)");
    println("  - accent: note [@A#2] - set note for accent beat (General MIDI, channel 10)");
    println("  - beat: note [@F#2]   - set note for regular beat (General MIDI, channel 10)");
    exit(1);
}

bpm = __param__.getOrDefault("bpm", "120").toInt();
if(typeOf(bpm) != "int") {
    throw "Expected 'bpm' to be of int type";
}

beats = __param__.getOrDefault("beats", "4").toInt();
if(typeOf(beats) != "int") {
    throw "Expected 'beats' to be of int type";
}

accent = __param__.getOrDefault("accent", "@A#2").toNote();
if(typeOf(accent) != "note") {
    throw "Expected 'accent' to be of note type";
}

beat = __param__.getOrDefault("beat", "@F#2").toNote();
if(typeOf(beat) != "note") {
    throw "Expected 'beat' to be of note type";
}

metronome(bpm, beats, accent, beat);

