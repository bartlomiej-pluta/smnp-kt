subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'kotlin-kapt'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group 'com.bartlomiejpluta'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencies {
        // compileOnly important!!! We do not want to put the api into the zip file since the main program has it already!
        compileOnly project(':api')
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        kapt(group: 'org.pf4j', name: 'pf4j', version: "3.2.0")
        compileOnly(group: 'org.pf4j', name: 'pf4j', version: "3.2.0") {
            exclude group: "org.slf4j"
        }
    }

    jar {
        manifest {
            attributes 'Plugin-Id': "${pluginId}",
                    'Plugin-Class': "${pluginClass}",
                    'Plugin-Version': "${pluginVersion}",
                    'Plugin-Provider': "${pluginProvider}",
                    'Plugin-Dependencies': "${pluginDependencies}"
        }
    }

    task plugin(type: Jar) {
        archiveBaseName = "plugin-${pluginId}"
        into('classes') {
            with jar
        }
        into('lib') {
            from configurations.compile
        }
        archiveExtension = 'zip'
    }

    task assemblePlugin(type: Copy) {
        from jar
        into pluginsDir
    }
}

task clean {
    dependsOn subprojects.clean
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

task build {
    dependsOn assemblePlugins
}