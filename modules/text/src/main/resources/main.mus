extend string {
    function find(char: string) {
        if(char.length > 1) {
            throw "Only single character can act as a pattern to be found";
        }

        this as (c, index) ^ {
            if(c == char) {
                return index;
            }
        }

        return -1;
    }

    function isEmpty() {
        return this.length == 0;
    }

    function isNotEmpty() {
        return not this.isEmpty();
    }

    function toInt() {
        if (this.isEmpty()) {
            return this;
        }

        digits = {
            "0" -> 0,
            "1" -> 1,
            "2" -> 2,
            "3" -> 3,
            "4" -> 4,
            "5" -> 5,
            "6" -> 6,
            "7" -> 7,
            "8" -> 8,
            "9" -> 9
        };

        str = this;
        negative = false;
        if(str.charAt(0) == "-") {
            negative = true;
            str = this.substring(1, this.length);
        }

        output = 0
        str as (char, index) ^ {
            if(not digits.containsKey(char)) {
                return this;
            }

            output = output + (digits.get(char) * 10 ** (str.length-index-1))
        }

        if (negative) {
            output = -output;
        }

        return Int(output);
    }

    function substring(startIndex: int, endIndex: int) {
        output = ""
        (endIndex - startIndex) as index ^ {
            output = output + this.charAt(index + startIndex);
        }

        return output;
    }

    function split(delimiter: string) {
        output = []

        previousDelimiter = 0;
        this as (char, index) ^ {
            if(char == delimiter) {
                output = output + [this.substring(previousDelimiter, index)];
                previousDelimiter = index+1;
            }
        }

        output = output + [this.substring(previousDelimiter, this.length)];

        return output;
    }

    function toFloat() {
        if (this.isEmpty()) {
            return this;
        }
        digits = {
            "0" -> 0,
            "1" -> 1,
            "2" -> 2,
            "3" -> 3,
            "4" -> 4,
            "5" -> 5,
            "6" -> 6,
            "7" -> 7,
            "8" -> 8,
            "9" -> 9
        };

        str = this;
        negative = false;
        if(str.charAt(0) == "-") {
            negative = true;
            str = this.substring(1, this.length);
        }

        split = str.split(".");
        if(split.size > 2) {
            return this;
        }

        iStr = split.get(0);
        dStr = split.get(1);

        i = 0;
        d = 0;

        iStr as (char, index) ^ {
            if(not digits.containsKey(char)) {
                return this;
            }

            i = i + (digits.get(char) * 10 ** (iStr.length-index-1))
        }

        dStr as (char, index) ^ {
            if(not digits.containsKey(char)) {
                return this;
            }

            d = d + (digits.get(char)) * 10 ** (-index-1);
        }

        number = i + d;
        if(negative) {
            number = -number;
        }

        return number;
    }

    function toBool() {
        if(this == "true") {
            return true;
        } else if (this == "false") {
            return false;
        } else {
            return this;
        }
    }

    function toNote() {
        pitch = "";
        octave = 4;
        duration = 4;
        dot = false;

        consumed = 0;
        if(this.length < 2) {
            return this;
        }

        # Note begin tag
        if(this.charAt(consumed) == "@") {
            consumed = consumed + 1;

            # Base pitch
            availablePitch = ["c", "d", "e", "f", "g", "a", "h", "b", "C", "D", "E", "F", "G", "A", "H", "B"]
            if((consumed < this.length) and availablePitch.contains(this.charAt(consumed))) {
                pitch = pitch + this.charAt(consumed);
                consumed = consumed + 1;

                # Flat or sharp
                if((consumed < this.length) and ["b", "#"].contains(this.charAt(consumed))) {
                    pitch = pitch + this.charAt(consumed);
                    consumed = consumed + 1;
                }

                # Exclude some invalid combination that passed previous filters
                if(["Bb", "bb", "Hb"].contains(pitch)) {
                    return this;
                }

                # Octave
                if(consumed < this.length) {
                    octave = this.charAt(consumed).toInt();
                    if(typeOf(octave) == "int") {
                        consumed = consumed + 1;
                    } else {
                        octave = 4;
                    }
                }

                # Duration start symbol
                if((consumed < this.length) and (this.charAt(consumed) == ":")) {
                    consumed = consumed + 1;
                    _duration = "";
                    (consumed < this.length and typeOf(this.charAt(consumed).toInt()) == "int") ^ {
                        _duration = _duration + this.charAt(consumed);
                        consumed = consumed + 1;
                    }
                    duration = _duration.toInt();

                    if(_duration.isEmpty()) {
                        return this;
                    }

                    dot = (consumed < this.length) and this.charAt(consumed) == "d";
                    if(dot) {
                        consumed = consumed+1;
                    }
                }

                # If some trailing characters remained
                if(consumed != this.length) {
                    return this;
                }

                return Note(pitch, octave, duration, dot);
            }
        }

        return this;
    }
}
