plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.10' apply false
}

configure(subprojects - project(':modules')) {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group 'com.bartlomiejpluta'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 17

    compileKotlin {
        kotlinOptions.jvmTarget = "17"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "17"
    }
}

ext.pluginsDir = rootProject.buildDir.path + '/modules'

repositories {
    mavenCentral()
}

task clean {
    dependsOn ':api:clean'
    dependsOn ':core:clean'
    dependsOn ':app:clean'
    dependsOn ':modules:clean'
}

task build {
    dependsOn ':api:build'
    dependsOn ':core:build'
    dependsOn ':modules:build'
    dependsOn ':app:jar'
}

task pack(type: Zip) {
    dependsOn build

    file('modules').eachDir { module ->
        from(new File(module, "build/libs")) {
            include "*.jar"
            into "/modules/"
        }
    }

    from("app/build/libs") {
        include "*.jar"
        into "/"
    }

    def outputDir = file('build/zip')
    outputDir.mkdir()

    archiveFileName = 'smnp.zip'
    destinationDirectory = outputDir
}